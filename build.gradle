// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
        maven{
            url "https://maven.google.com"
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:24.1-jre'
        classpath 'com.android.tools.build:gradle:3.3.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task startAppEngine { dependsOn ":backend:appengineStart"
    doLast {
        println "Task [startAppEngine] completed >> App Engine started"
    }
}

task runAndroidTest { dependsOn ":app:connectedAndroidTest"
    doLast {
        println "Task [runAndroidTest] completed >> Test reports can be found at project-folder/app/build/reports directory"
    }
}

task stopAppEngine { dependsOn ":backend:appengineStop"
    doLast {
        println "Task [stopAppEngine] completed >> App Engine stopped"
    }
}

task jokeAppCustomTask { dependsOn "startAppEngine", "runAndroidTest"
                         mustRunAfter "startAppEngine"
                         finalizedBy "stopAppEngine"
    doLast {
        println "Task [jokeAppCustomTask] completed >> Task cycle completed"
    }
}

runAndroidTest.mustRunAfter "startAppEngine"
runAndroidTest.finalizedBy "stopAppEngine"


allprojects {
    repositories {
        jcenter()
        google()
    }

}
